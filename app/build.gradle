plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk androidCompileSdk

    defaultConfig {
        applicationId "com.gavin.octopus"
        minSdk androidMinSdk
        targetSdk androidTargetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'launch'
    productFlavors {
        //完整版
        full {
            dimension "launch"
            resValue("string", "app_name", "Octopus")
        }

        demo {
            dimension "launch"
            resValue("string", "app_name", "OctopusDemo")
            applicationIdSuffix '.demo'
        }

        videoDetail {
            dimension "launch"
            resValue("string", "app_name", "OctopusVideoDetail")
            applicationIdSuffix '.videoDetail'
        }
    }

    sourceSets {
        ['demo', 'videoDetail'].each { b ->
            "$b" {
                manifest.srcFile 'src/launcher/AndroidManifest.xml'
                java.srcDirs += 'src/launcher/java'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    videoDetailImplementation project(':launcher:video-detail')
    demoImplementation project(':launcher:demo')

    fullImplementation project(':launcher:video-detail')

    implementation project(':framework')
    implementation project(':common')

    implementation google.material

    debugImplementation android_libs.compose_ui_tooling

    testImplementation tests.junit

    kapt third_libs.arouter_compiler
}